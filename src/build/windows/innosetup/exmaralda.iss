; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
ArchitecturesInstallIn64BitMode=x64

AppName=EXMARaLDA
AppVerName=EXMARaLDA @version@
AppPublisher=Thomas Schmidt, Kai Woerner
AppPublisherURL=http://www.exmaralda.org
AppSupportURL=http://www.exmaralda.org
AppUpdatesURL=http://www.exmaralda.org

DefaultDirName={pf}\EXMARaLDA
DefaultGroupName=EXMARaLDA
DisableDirPage=no

LicenseFile=@templatesdir@\windows\innosetup\copyright_exmaralda.txt
InfoAfterFile=@templatesdir@\windows\innosetup\releasenotes_exmaralda.txt
SetupIconFile=exmaralda_install.ico
WizardImageFile=exmaralda_install_wizard.bmp
WizardSmallImageFile=exmaralda_install.bmp
ChangesAssociations=yes
VersionInfoCompany=Archiv für Gesprochenes Deutsch, IDS Mannheim
VersionInfoCopyright=Thomas Schmidt, Kai Wörner
VersionInfoDescription=Version @version@ (Build time: @build-time@)
VersionInfoVersion=@version@ 

[Types]
Name: "full"; Description: "Full installation (all tools)"
Name: "compact"; Description: "Compact installation (almost all tools)"
Name: "EXMARaLDAOnly"; Description: "EXMARaLDA tools only"
Name: "FOLKOnly"; Description: "FOLK tools only"
Name: "Custom"; Description: "Custom (make your own choice)"; Flags: iscustom

[Components]
Name: "main"; Description: "Main Files"; Types: full compact EXMARaLDAOnly FOLKOnly; Flags: fixed
Name: "EXMARaLDA"; Description: "EXMARaLDA tools"; Types: full
Name: "EXMARaLDA\PartiturEditor"; Description: "Partitur-Editor"; Types: full compact EXMARaLDAOnly
Name: "EXMARaLDA\CorpusManager"; Description: "Corpus-Manager"; Types: full compact EXMARaLDAOnly
Name: "EXMARaLDA\EXAKT"; Description: "EXAKT"; Types: full compact EXMARaLDAOnly
Name: "EXMARaLDA\TEIDrop"; Description: "TEI-Drop"; Types: full EXMARaLDAOnly
Name: "EXMARaLDA\Sextant"; Description: "Sextant"; Types: full EXMARaLDAOnly
Name: "FOLK"; Description: "FOLK tools"; Types: full
Name: "FOLK\FOLKER"; Description: "FOLKER editor"; Types: full compact FOLKOnly
Name: "FOLK\OrthoNormal"; Description: "OrthoNormal"; Types: full compact FOLKOnly



[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: {cm:AdditionalIcons}
Name: associate; Description: "{cm:AssocFileExtension,Partitur-Editor,exb}"; GroupDescription: "Other tasks:"; Flags: checkedonce;
Name: associateComa; Description: "{cm:AssocFileExtension,Coma,coma}"; GroupDescription: "Other tasks:"; Flags: checkedonce;


[Files]
; ************************ New: Result from jpackage!
Source: "@tempdir@\EXMARaLDA\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs
Source: "D:\EXMARaLDA_GIT\exmaralda\src\build\windows\icons\*document*.ico"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs
Source: "D:\EXMARaLDA_GIT\exmaralda\src\build\windows\icons\exmaralda.ico"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs



[Languages]
Name: en; MessagesFile: "compiler:Default.isl"
Name: de; MessagesFile: "compiler:Languages\German.isl"
Name: fr; MessagesFile: "compiler:Languages\French.isl"
Name: nl; MessagesFile: "compiler:Languages\Dutch.isl"
Name: pt; MessagesFile: "compiler:Languages\Portuguese.isl"
Name: es; MessagesFile: "compiler:Languages\Spanish.isl"
Name: ru; MessagesFile: "compiler:Languages\Russian.isl"
Name: it; MessagesFile: "compiler:Languages\Italian.isl"
Name: dk; MessagesFile: "compiler:Languages\Danish.isl"
Name: cz; MessagesFile: "compiler:Languages\Czech.isl"


[INI]
Filename: "{app}\exmaralda.url"; Section: "InternetShortcut"; Key: "URL"; String: "http://www.exmaralda.org"

[Icons]
; ************************ for the program menu (?)
Name: "{group}\Partitur-Editor"; IconFilename: "{app}\PartiturEditor.ico"; Filename: "{app}\PartiturEditor.exe";  Components: EXMARaLDA\PartiturEditor
Name: "{group}\COMA"; IconFilename: "{app}\Coma.ico"; Filename: "{app}\Coma.exe"; Components: EXMARaLDA\CorpusManager
Name: "{group}\EXAKT"; IconFilename: "{app}\EXAKT.ico"; Filename: "{app}\EXAKT.exe"; Components: EXMARaLDA\EXAKT
Name: "{group}\TEI Drop"; IconFilename: "{app}\TeidDrop.ico"; Filename: "{app}\TeiDrop.exe"; Components: EXMARaLDA\TEIDrop
Name: "{group}\FOLKER"; IconFilename: "{app}\FOLKER.ico"; Filename: "{app}\FOLKER.exe"; Components: FOLK\FOLKER
Name: "{group}\OrthoNormal"; IconFilename: "{app}\OrthoNormal.ico"; Filename: "{app}\OrthoNormal.exe"; Components: FOLK\OrthoNormal
Name: "{group}\EXMARaLDA on the Web"; IconFilename: "{app}\exmaralda.ico"; Filename: "{app}\exmaralda.url"
Name: "{group}\Uninstall EXMARaLDA"; Filename: "{uninstallexe}"

; ************************ for the desktop
Name: "{userdesktop}\Partitur-Editor"; IconFilename: "{app}\PartiturEditor.ico"; Filename: "{app}\PartiturEditor.exe"; Tasks: desktopicon;  Components: EXMARaLDA\PartiturEditor
Name: "{userdesktop}\Coma"; IconFilename: "{app}\coma.ico"; Filename: "{app}\Coma.exe"; Tasks: desktopicon; Components: EXMARaLDA\CorpusManager
Name: "{userdesktop}\EXAKT"; IconFilename: "{app}\exakt.ico"; Filename: "{app}\EXAKT.exe"; Tasks: desktopicon; Components: EXMARaLDA\EXAKT
Name: "{userdesktop}\FOLKER"; IconFilename: "{app}\folker.ico"; Filename: "{app}\FOLKER.exe"; Tasks: desktopicon; Components: FOLK\FOLKER
Name: "{userdesktop}\OrthoNormal"; IconFilename: "{app}\orthonormal.ico"; Filename: "{app}\OrthoNormal.exe"; Tasks: desktopicon; Components: FOLK\OrthoNormal



[Registry]
; ************************ EXB --> Partitur-Editor
Root: HKCR; Subkey: ".exb"; ValueType: string; ValueName: ""; ValueData: "ExmaraldaTranscriptionFile"; Flags: uninsdeletevalue; Tasks: associate;
Root: HKCR; Subkey: "ExmaraldaTranscriptionFile"; ValueType: string; ValueName: ""; ValueData: "Exmaralda Basic Transcription"; Flags: uninsdeletekey; Tasks: associate;
Root: HKCR; Subkey: "ExmaraldaTranscriptionFile\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\pe-document-icon.ico"; Tasks: associate;
Root: HKCR; Subkey: "ExmaraldaTranscriptionFile\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\PartiturEditor.exe"" ""%1"""; Tasks: associate;
; ************************ COMA --> COMA
Root: HKCR; Subkey: ".coma"; ValueType: string; ValueName: ""; ValueData: "ComaFile"; Flags: uninsdeletevalue; Tasks: associateComa;
Root: HKCR; Subkey: "ComaFile"; ValueType: string; ValueName: ""; ValueData: "Exmaralda Corpus"; Flags: uninsdeletekey; Tasks: associateComa;
Root: HKCR; Subkey: "ComaFile\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\comadocument.ico"; Tasks: associateComa;
Root: HKCR; Subkey: "ComaFile\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\Coma.exe"" ""%1"""; Tasks: associateComa;
; ************************ FLK --> FOLKER
Root: HKCR; Subkey: ".flk"; ValueType: string; ValueName: ""; ValueData: "FolkerTranscriptionFile"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "FolkerTranscriptionFile"; ValueType: string; ValueName: ""; ValueData: "Folker Transcription File"; Flags: uninsdeletekey
Root: HKCR; Subkey: "FolkerTranscriptionFile\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\folker-document-icon-128.ico"
Root: HKCR; Subkey: "FolkerTranscriptionFile\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\FOLKER.exe"" ""%1"""
; ************************ FLN --> OrthoNormal
Root: HKCR; Subkey: ".fln"; ValueType: string; ValueName: ""; ValueData: "NormalizedFolkerTranscription"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "NormalizedFolkerTranscription"; ValueType: string; ValueName: ""; ValueData: "Normalized Folker Transcription"; Flags: uninsdeletekey
Root: HKCR; Subkey: "NormalizedFolkerTranscription\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\orthonormal.ico"
Root: HKCR; Subkey: "NormalizedFolkerTranscription\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\OrthoNormal.exe"" ""%1"""
; ************************ EXS --> (Not double clickable?)
Root: HKCR; Subkey: ".exs"; ValueType: string; ValueName: ""; ValueData: "ExmaraldaSegmentedTranscriptionFile"; Flags: uninsdeletevalue; Tasks: associate;
Root: HKCR; Subkey: "ExmaraldaSegmentedTranscriptionFile"; ValueType: string; ValueName: ""; ValueData: "Exmaralda Segmented Transcription"; Flags: uninsdeletekey; Tasks: associate;
Root: HKCR; Subkey: "ExmaraldaSegmentedTranscriptionFile\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\segmented_document.ico"; Tasks: associate;
; ************************ EXC --> (Not double clickable?)
Root: HKCR; Subkey: ".exc"; ValueType: string; ValueName: ""; ValueData: "ExmaraldaConcordance"; Flags: uninsdeletevalue; Tasks: associate;
Root: HKCR; Subkey: "ExmaraldaConcordance"; ValueType: string; ValueName: ""; ValueData: "EXAKT Concordance"; Flags: uninsdeletekey; Tasks: associate;
Root: HKCR; Subkey: "ExmaraldaConcordance\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\exakt.ico"; Tasks: associate;
; ************************ EXA --> (Not double clickable?)
Root: HKCR; Subkey: ".exa"; ValueType: string; ValueName: ""; ValueData: "SextantAnnotationFile"; Flags: uninsdeletevalue; Tasks: associate;
Root: HKCR; Subkey: "SextantAnnotationFile"; ValueType: string; ValueName: ""; ValueData: "Sextant Annotation File"; Flags: uninsdeletekey; Tasks: associate;
Root: HKCR; Subkey: "SextantAnnotationFile\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\sextant-document.ico"; Tasks: associate;

[Run]
Filename: "{app}\PartiturEditor.exe"; Description: "{cm:LaunchProgram, Partitur-Editor}"; Flags: shellexec postinstall skipifsilent

[UninstallDelete]
Type: files; Name: "{app}\exmaralda.url"

